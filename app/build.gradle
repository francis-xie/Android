apply plugin: 'com.android.application'
apply plugin: 'com.basic.aop' //引用aop插件
apply plugin: 'com.basic.router' //引用router-plugin插件实现自动注册
apply plugin: 'img-optimizer'
if (isNeedPackage.toBoolean() && isUseBooster.toBoolean()) {
    apply plugin: 'com.didiglobal.booster'
}

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.basic.code"
        minSdkVersion 17
        targetSdkVersion build_versions.target_sdk
        versionCode 20
        versionName "1.1.9"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'x86'
        }
    }

    if (isNeedPackage.toBoolean()) {
        signingConfigs {
            release {
                storeFile file(app_release.storeFile)
                storePassword app_release.storePassword
                keyAlias app_release.keyAlias
                keyPassword app_release.keyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isNeedPackage.toBoolean()) {
                signingConfig signingConfigs.release

                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                buildConfigField "String", "APP_ID_BUGLY", properties.getProperty("APP_ID_BUGLY")
                buildConfigField "String", "APP_ID_UMENG", properties.getProperty("APP_ID_UMENG")
            }
        }

        debug {
            debuggable true
            minifyEnabled false

            buildConfigField "String", "APP_ID_BUGLY", '""'
            buildConfigField "String", "APP_ID_UMENG", '""'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.androidx.multidex
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    //下拉刷新、CityPicker、FACE框架需要
//    implementation deps.androidx.appcompat
//    implementation deps.androidx.recyclerview
//    implementation deps.androidx.design
//    implementation deps.glide
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    // FACE框架
    implementation project(':face-api')

    // 工具类
    implementation project(':tools-core')
    implementation project(':tools-api')
    // 切片
    implementation project(':aop-api')
    // 页面路由
    implementation project(':router-api')
    annotationProcessor project(':router-compiler')
    // Page页面框架
    implementation project(':page-api')
    annotationProcessor project(':app-compiler')
    // ButterKnife
    implementation deps.butterknife.runtime
    annotationProcessor deps.butterknife.compiler
    // LeakCanary内存泄漏检测
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'

    // 弹性布局
    implementation 'com.google.android:flexbox:0.3.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    // 悬浮按钮
    implementation 'com.github.clans:fab:1.6.4'
    // rxjava2
    implementation deps.rxjava2
    implementation deps.rxandroid
    // RxBinding的sdk
    implementation deps.rxbinding
    implementation project(':util-api')
    // 任务框架
    implementation project(':job-core')

    // PictureSelector图片选择
    implementation project(':image-core')
    //需要裁剪时使用
    //implementation project(':image-api')

    // 下拉刷新
    implementation project(':refresh-header')
    implementation project(':refresh-layout')

    // 列表侧滑菜单
    implementation 'com.yanzhenjie.recyclerview:support:1.3.2'

    // 预加载占位控件
    implementation 'me.samlss:broccoli:1.0.0'

    // WebView
    implementation project(':web-core')
    implementation project(':web-download')//选填
    implementation project(':web-api')//选填

    // 数据库
    implementation project(':dbms-api')
    annotationProcessor project(':dbms-compiler')

    // 侧边栏菜单
    implementation 'com.yarolegovich:sliding-root-nav:1.1.0'

    // Scan二维码扫描
    implementation project(':scan-api')
    // CityPicker
    implementation project(':cities-api')
    // Floats悬浮窗口
    implementation project(':float-api')
    // android进程监控
    implementation 'com.jaredrummler:android-processes:1.1.1'

    // Android图表库
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    // ECharts图表库
    implementation 'com.github.abel533:ECharts:3.0.0.2'
    // TabBar
    implementation project(':tag-api')
    // 日历控件
    implementation project(':date-api')
    implementation 'com.haibin:calendarview:3.6.3'
    // 图标库
    implementation "com.mikepenz:iconics-core:4.0.2"
    // 图标库控件（可选）
    implementation "com.mikepenz:iconics-views:4.0.2"
    // 相机拍摄
    implementation project(':cameraview')
    // 另一款相机拍摄库，使用更简单
    implementation 'com.wonderkiln:camerakit:0.13.1'

    // 阿里巴巴组件
    implementation 'com.alibaba.android:vlayout:1.3.0'
    implementation 'com.alibaba.android:ultraviewpager:1.0.7.8'
    implementation 'com.alibaba.android:tangram:3.3.6'
    // 版本更新
    implementation project(':renew-api')
    implementation 'com.zhy:okhttputils:2.6.2'
    implementation deps.gson
    implementation deps.okhttp3
    // 腾讯的键值对存储mmkv
    implementation 'com.tencent:mmkv:1.2.10'
    // 腾讯x5TBS
    implementation 'com.tencent.tbs.tbssdk:sdk:43967'
    // 富文本
    implementation 'com.zzhoujay.richtext:richtext:3.0.8'

    // 双列表联动
    implementation 'com.kunminx.linkage:linkage-recyclerview:1.9.2'

    // umeng统计
    implementation 'com.umeng.umsdk:common:9.3.8'
    implementation  'com.umeng.umsdk:asms:1.2.1'

    // ANR异常捕获
    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'
}
